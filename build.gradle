plugins {
    id "java"
    id "idea"
    id "org.jetbrains.intellij" version "1.9.0"
    id "pl.allegro.tech.build.axion-release" version "1.14.1"
}

group "com.ensighen"

scmVersion {
    tag {
        prefix = "v"
        versionSeparator = ""
    }
}

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }
    project.version = scmVersion.version

    ext.jetbrains = [
            version    : "IU-2022.2",
            pycharm    : "PythonCore:222.3345.40",
            rubymine   : "org.jetbrains.plugins.ruby:222.3345.16",
            goland     : "org.jetbrains.plugins.go:222.3345.90",
            nodejs     : "NodeJS:222.3345.3"
    ]
}

intellij {
    version = jetbrains.version
    pluginName = "Aws-Okta"
    sameSinceUntilBuild = false
    updateSinceUntilBuild = false
    downloadSources = true
    plugins = [
            // This doesn't make any sense as this plugin supposed to be only required on IDEA sub-module
            // but it doesn't build without it. Suspect a validation bug in JetBrains Gradle plugin.
            "java",
    ]
}
buildSearchableOptions.enabled = false

dependencies {
    implementation project(":awsokta-products-idea")
    implementation project(":awsokta-products-pycharm")
    implementation project(":awsokta-products-rubymine")
    implementation project(":awsokta-products-goland")
    implementation project(":awsokta-products-nodejs")
}

wrapper {
    gradleVersion = "7.5.1"
}

runIde.ideDir =  java.util.Optional.ofNullable(System.getenv("IDE")).map(File::new).orElse(null)

tasks {
    patchPluginXml {
        sinceBuild.set("201")
    }

}
